cmake_minimum_required(VERSION 3.15)

project(opensfm C CXX)

if(NOT CMAKE_BUILD_TYPE)
  set(CMAKE_BUILD_TYPE Release CACHE STRING
      "Choose the type of build, options are: None Debug Release RelWithDebInfo MinSizeRel."
      FORCE)
endif()
set(CMAKE_MODULE_PATH ${opensfm_SOURCE_DIR}/cmake)

cmake_policy(SET CMP0063 NEW)
# Re-activate once we can have a decent version of CMake (> 3.27)
# cmake_policy(SET CMP0148 NEW)

if (WIN32)
# Place compilation results in opensfm/ folder, not in Debug/ or Release/
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_RELEASE "${opensfm_SOURCE_DIR}/..")
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY_DEBUG "${opensfm_SOURCE_DIR}/..")
endif()

####### Compilation Options #######
# Visibility stuff
set(CMAKE_CXX_VISIBILITY_PRESET hidden)
set(CMAKE_CXX_VISIBILITY_INLINES ON)

# fPIC
set(CMAKE_POSITION_INDEPENDENT_CODE ON)

# C++17
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# Enable all warnings
if (NOT WIN32)
set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wall -Wextra -Wno-sign-compare")
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -Wextra -Wno-sign-compare")
endif()

# For compiling VLFeat
add_definitions(-DVL_DISABLE_AVX)

# We're not using an inplace Glog
add_definitions(-DGLOG_USE_GLOG_EXPORT)

# Use the version of vlfeat in ./src/third_party/vlfeat
add_definitions(-DINPLACE_VLFEAT)
if(NOT USE_SSE2)
  add_definitions(-DVL_DISABLE_SSE2)
endif()

if (WIN32)
    # Missing math constant
    add_definitions(-DM_PI=3.14159265358979323846)
endif()

####### Find Dependencies #######
find_package(OpenMP)
if (OPENMP_FOUND)
  set (CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${OpenMP_C_FLAGS}")
  set (CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${OpenMP_CXX_FLAGS}")
endif()

find_package(LAPACK)
find_package(SuiteSparse)
find_package(Eigen3 REQUIRED)
find_package(Ceres)
find_package(Gflags REQUIRED)

# Try to find glog using its native CMake config first (modern systems)
# Fall back to custom FindGlog.cmake for older systems (e.g., Ubuntu 20.04)
find_package(glog QUIET CONFIG)
if(glog_FOUND)
  message(STATUS "Using glog native CMake config (modern glog)")
else()
  message(STATUS
    "glog CMake config not found, using FindGlog.cmake "
    "(Ubuntu 20.04 compatibility)"
  )
  find_package(Glog REQUIRED)
endif()

# Ceres2 exposes Ceres::ceres target.
# Ceres1 exposes just ceres.
# - if there's no such target, cmake will convert it into -lceres
# and the linker will fail
if(Ceres_ceres_FOUND)
    set(CERES_LIBRARIES Ceres::ceres)
else()
    set(CERES_LIBRARIES ceres)
endif()

# Only link against the OpenCV components we actually need
# This avoids pulling in unnecessary dependencies like viz (which requires VTK)
# OpenCV's OpenCVConfig will enforce imgcodecs for < 3.0
# (even if OPTIONAL_COMPONENTS) so we handle version detection carefully
# Cause is imread/imwrite moved to imgcodecs on > 3.0
find_package(OpenCV QUIET)
if(OpenCV_FOUND AND ${OpenCV_VERSION} VERSION_LESS "3.0")
    find_package(OpenCV REQUIRED COMPONENTS core imgproc calib3d)
else()
    find_package(OpenCV REQUIRED COMPONENTS core imgproc calib3d
                 OPTIONAL_COMPONENTS imgcodecs)
endif()

# Override OpenCV_LIBS to only include the components we explicitly requested
# This prevents linking against all OpenCV modules
# (especially viz which requires VTK)
set(OpenCV_LIBS opencv_core opencv_imgproc opencv_calib3d)
if(TARGET opencv_imgcodecs)
    list(APPEND OpenCV_LIBS opencv_imgcodecs)
endif()

####### Third party libraries #######
add_subdirectory(third_party/pybind11)
add_subdirectory(third_party/akaze)
add_subdirectory(third_party/vlfeat)

include_directories(third_party/vlfeat)

####### Debugging #######
if (CMAKE_BUILD_TYPE MATCHES DEBUG)
    add_executable(debug_c_extension
        debug_c_extension.cc
    )
    target_link_libraries(debug_c_extension PRIVATE pybind11::embed)
endif()


####### Testing #######
option(OPENSFM_BUILD_TESTS "Build OpenSfM unit tests." on)

if (OPENSFM_BUILD_TESTS)
  enable_testing()
  include_directories(third_party/gtest)
  add_definitions(-DCERES_GFLAGS_NAMESPACE=${GFLAGS_NAMESPACE})

  # Gtest if old now and trigger these
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wno-missing-field-initializers")
  set(CMAKE_CXX_FLAGS
      "${CMAKE_CXX_FLAGS} -Wno-deprecated-copy-with-user-provided-copy"
  )

  add_library(gtest
              third_party/gtest/gmock_gtest_all.cc
              third_party/gtest/gmock_main.cc)
  target_include_directories(gtest PRIVATE ${GFLAGS_INCLUDE_DIR})
  target_link_libraries(gtest PRIVATE glog::glog)

  set(TEST_MAIN test_main)
  add_library(${TEST_MAIN} testing_main.cc)
  target_link_libraries(${TEST_MAIN}
                        ${GFLAGS_LIBRARY}
                        glog::glog
                        gtest)
endif()

####### OpenSfM libraries #######
add_subdirectory(foundation)
add_subdirectory(bundle)
add_subdirectory(dense)
add_subdirectory(features)
add_subdirectory(geo)
add_subdirectory(geometry)
add_subdirectory(robust)
add_subdirectory(sfm)
add_subdirectory(map)
